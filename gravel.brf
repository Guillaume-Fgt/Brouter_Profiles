---context:global   # following code refers to global config
assign processUnusedTags 1  # set to true if you want to display all tags in the "data"

assign validForBikes true
assign validForCars false
assign validForFoot false

---context:way   # following code refers to way-tags

#forbidden segments (cost=10000)
#---------------------------
assign bad_paths 
	switch and highway=path smoothness=impassable|very_horrible|horrible true
	switch and highway=path sac_scale=difficult_alpine_hiking|demanding_alpine_hiking|alpine_hiking|demanding_mountain_hiking|mountain_hiking|hiking true
	switch and highway=path or mtb:scale=2|2+|2-|3|4|5|6 mtb:scale:uphill=2|3|4|5 true
	false
    
assign bad_oneway
	switch and reversedirection=yes and oneway=yes not oneway:bicycle=no true
	switch and reversedirection=yes junction=roundabout|circular true
    	false

assign bad_highway
	switch and highway=footway|bridleway|pedestrian not bicycle=yes true
	switch highway=motorway|trunk|motorway_link|trunk_link|raceway|corridor true
	switch bicycle=no true
	false

assign no_access
	switch and access=no|private not bicycle=yes|permissive|designated|dismount true
	false

#initial costs
#---------------------------
assign initialclassifier 
	switch route=ferry	1
	switch bad_paths	2
    	switch bad_oneway	3
	switch bad_highway	4
	switch no_access	5
    	22

assign initialcost 
	switch route=ferry	10000
	switch bad_paths	10000
    	switch bad_oneway	10000
	switch bad_highway	10000
	switch no_access	10000
    	0

#turncost
#---------------------------
#is implicitely added to costfactor
# this is the cost (in Meter) for a 90-degree turn
# The actual cost is calculated as turncost*cos(angle)
assign turncost = 15

#surface (we want good unpaved first)
#---------------------------
assign fav_surface
	switch surface=fine_gravel|compacted  1.0
	switch surface=ground|dirt|earth|gravel|fine_gravel 1.1
	switch surface=asphalt|concrete|paving_stones|wood|sett 1.2
	switch surface=pebblestone|dirt|mud|sand|clay 1.3
	1.5

#highway types (we want track and unclassified first)
#---------------------------
assign fav_highway
	switch highway=track|path 1.0
	switch highway=unclassified|service|tertiary|tertiary_link 1.1
	switch highway=living_street|residential|cycleway 1.2
	switch highway=secondary|secondary_link 1.3
	switch highway=primary|primary_link 1.4
	switch highway=footway|bridleway|pedestrian 1.5
	1.6

#track types (we want track with a correct grade)
#---------------------------
assign fav_tracktype
	switch tracktype=grade1 1.0
	switch tracktype=grade2 1.1
	switch tracktype=grade3 1.2
	switch tracktype=grade4 1.3
	switch tracktype=grade5 1.4
	1.2

#smoothness types (worst ones are already forbidden)
#---------------------------
assign fav_smoothness
	switch smoothness=excellent|good 1.0
	switch smoothness=intermediate 1.1
	switch smoothness=bad 1.2
	switch smoothness=very_bad 1.3
	1.2

#cycle routes (we want a small bonus if it's one)
#---------------------------
#icn=international, ncn=national, rcn=regional, lcn=local
assign fav_cycleroute
	switch or route_bicycle_icn=yes or route_bicycle_ncn=yes or route_bicycle_rcn=yes route_bicycle_lcn=yes 1.0 1.2

#oneway (we want to avoid taking one way streets in opposite direction if it's cheap to avoid it)
#---------------------------
assign fav_oneway
	switch and reversedirection=yes and oneway=yes oneway:bicycle=no 1.4 1.0

assign costfactor
	multiply fav_surface 
	multiply fav_highway 
	multiply fav_cycleroute 
	multiply fav_oneway
	multiply fav_tracktype
	fav_smoothness

---context:node  # following code refers to node tags


#barriers
#---------------------------
assign barrierpenalty
	switch barrier=					0.0
	switch barrier=gate|swing_gate|lift_gate  	1.1
	switch barrier=block|bollard    		1.4
	3.0

assign initialcost 10

assign initialcost
	multiply initialcost barrierpenalty
