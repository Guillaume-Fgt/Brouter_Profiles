---context:global   # following code refers to global config
assign processUnusedTags 1  # set to true if you want to display all tags in the "data"

assign validForBikes true
assign validForCars false
assign validForFoot false

#Elevation
#---------------------------
assign downhillcost	20
assign uphillcost	25


#Options
#---------------------------
assign avoid_hiking = false	# %avoid_hiking% | if you want to avoid hiking routes | boolean
assign safe_gate = false	# %safe_gate% | avoid gates to not encounter closed parcs/public gardens...  | boolean


---context:way   # following code refers to way-tags

#forbidden segments (cost=10000)
#---------------------------
assign bad_sac_scale
	sac_scale=difficult_alpine_hiking|demanding_alpine_hiking|alpine_hiking|demanding_mountain_hiking|mountain_hiking|hiking
	
assign bad_paths 
	switch and highway=path smoothness=impassable|very_horrible|horrible		true
	switch and highway=path bad_sac_scale						true
	switch and highway=path or mtb:scale=2|2+|2-|3|4|5|6 mtb:scale:uphill=2|3|4|5	true
	false
    
assign bad_oneway
	switch and reversedirection=yes and oneway=yes not oneway:bicycle=no	true
	switch and reversedirection=yes junction=roundabout|circular		true
    	false

assign bad_highway
	switch and highway=footway|bridleway|pedestrian not bicycle=yes|designated	true
	switch highway=motorway|trunk|motorway_link|trunk_link|raceway|corridor		true
	switch bicycle=no true
	false

assign no_access
	switch and access=no|private not bicycle=yes|permissive|designated|dismount	true
	switch waterway=								true
	false

assign high_maxspeed
	switch maxspeed=130|120|110 true
	false

#initial costs
#---------------------------
assign initialclassifier 
	switch route=ferry	1
	switch bad_paths	2
	switch bad_oneway	3
	switch bad_highway	4
	switch no_access	5
	switch high_maxspeed	6
    	22

assign initialcost 
	switch route=ferry	10000
	switch bad_paths	10000
	switch bad_oneway	10000
	switch bad_highway	10000
	switch no_access	10000
	switch high_maxspeed	10000
    	0

#turncost
#---------------------------
#is implicitely added to costfactor
# this is the cost (in Meter) for a 90-degree turn
# The actual cost is calculated as turncost*cos(angle)
assign turncost = 15

#surface (good unpaved first)
#---------------------------
assign fav_surface
	switch surface=fine_gravel|compacted				1.0
	switch surface=ground|dirt|earth|gravel|fine_gravel|cobblestone	1.1
	switch surface=asphalt|concrete|paving_stones|wood|sett		1.2
	switch surface=paved						1.3
	switch surface=pebblestone|mud|sand|clay			1.4
	switch surface=unpaved						1.4
	1.5

#highway types
#---------------------------
assign fav_highway
	switch highway=track						1.0
	switch highway=path 						1.1
	switch highway=unclassified|service				1.2
	switch highway=footway|pedestrian|living_street|residential	1.3
	switch highway=tertiary|tertiary_link|bridleway|cycleway	1.4
	switch highway=secondary|secondary_link 			1.5
	switch highway=primary|primary_link 				1.6
	1.6

#track types
#---------------------------
assign fav_tracktype
	switch highway=track|service|path|unclassified #only tags where tracktype has a meaning
	switch tracktype=grade1 1.0
	switch tracktype=grade2 1.1
	switch tracktype=grade3 1.2
	switch tracktype=grade4 1.3
	switch tracktype=grade5 1.4
	1.2
	1.0

#smoothness types (worst ones are already forbidden)
#---------------------------
assign fav_smoothness
	switch smoothness=excellent|good|intermediate	1.0
	switch smoothness=bad				1.1
	switch smoothness=very_bad			1.3
	1.2

#cycle routes (we want a small bonus if it's one)
#---------------------------
#icn=international, ncn=national, rcn=regional, lcn=local
assign fav_cycleroute
	switch	or route_bicycle_icn=yes
		or route_bicycle_ncn=yes
		or route_bicycle_rcn=yes
		route_bicycle_lcn=yes 1.0 
		1.1

#hiking routes (we want a small malus if it's one and option was ticked)
#---------------------------
#walking network
assign unfav_hiking
	switch avoid_hiking
	switch or route_hiking_iwn=yes or route_hiking_nwn=yes or route_hiking_rwn=yes route_hiking_lwn=yes 1.1 
	1.0
	1.0

#oneway (we want to avoid taking one way streets in opposite direction if it's cheap to avoid it)
#---------------------------
assign avoid_oneway
	switch and reversedirection=yes and oneway=yes oneway:bicycle=no 1.2 1.0

#avoid highspeed roads (highest ones already forbidden)
#---------------------------
assign fav_lowspeed
	switch maxspeed=20|30|40|50							1.0
	switch maxspeed=60|70								1.1
	switch maxspeed=80|90								1.2
	switch and maxspeed= highway=tertiary|tertiary_link 				1.1 
	switch and maxspeed= highway=primary|primary_link|secondary|secondary_link 	1.2
	1.0

assign costfactor
	multiply fav_surface 
	multiply fav_highway 
	multiply fav_cycleroute 
	multiply avoid_oneway
	multiply fav_tracktype
	multiply fav_smoothness
	multiply unfav_hiking
	fav_lowspeed

---context:node  # following code refers to node tags

#forbidden nodes
#---------------------------

assign no_access
	switch access=no|private	1000000
	switch bicycle=no		1000000
	1.0

#barriers
#---------------------------
assign barrierpenalty
	switch barrier=swing_gate|motorcycle_barrier|block|bollard|cycle_barrier|kissing_gate|kerb	1
	switch barrier=gate		switch safe_gate						1000000 5
	switch barrier=lift_gate	switch safe_gate						1000000 15
	switch barrier=											0
	20

assign initialcost 0

assign initialcost
	add initialcost
	multiply add initialcost 10
	multiply barrierpenalty
	no_access
